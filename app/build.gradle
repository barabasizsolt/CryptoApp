plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'io.gitlab.arturbosch.detekt'
    id "org.jlleitschuh.gradle.ktlint"
    id "org.jmailen.kotlinter"
}

def keystorePropertiesFile = rootProject.file("keystores.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.example.cryptoapp"
        minSdk 29
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kotlinOptions.jvmTarget = '1.8'
}

check {
    dependsOn "installKotlinterPrePushHook"
}

dependencies {
    //Serialization dependency
    implementation 'com.google.code.gson:gson:2.8.8'

    //Image loading dependency
    def coil_version = "1.4.0"
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"

    //Networking dependencies
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'

    /*-------------------Database dependencies-----------------*/
    //Room dependency
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //Firebase dependencies
    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-auth:21.0.1'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    //Architectural dependencies
    def lifecycle_version = "2.4.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    /*-------------------UI Layer dependencies-----------------*/
    //Card view dependency
    implementation 'androidx.cardview:cardview:1.0.0'
    //Recyclerview dependency
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    //Material design dependency
    implementation 'com.google.android.material:material:1.4.0'
    //Splash screen dependency
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'
    //Constraint Layout dependency
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    //DrawerLayout dependency
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'
    //AnyChart dependency
    implementation 'com.github.AnyChart:AnyChart-Android:0.3.0'

    /*-------------------Android Core dependencies-----------------*/
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'

    /*-------------------Android Test dependencies-----------------*/
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}